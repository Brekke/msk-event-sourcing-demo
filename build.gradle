// Common Project Configuration -----------------------------------------------

allprojects {
  group = 'software.amazon.samples'
  version = '0.0.1'
  repositories {
    mavenLocal()
    mavenCentral()
  }
}

subprojects {
  apply plugin: 'java'
  java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }
  clean {
    delete 'cdk.out'
  }
}

// Dependency Versions --------------------------------------------------------

ext {
  cdkVersion = '1.20.0'
  simpleSourcingVersion = '0.2.7'
  junitVersion = '4.12'
}

// Sub Project Configurations -------------------------------------------------

project(':cdk-pipeline') {
  apply plugin: 'application'
  dependencies {
    compile "software.amazon.awscdk:iam:${cdkVersion}"
    compile "software.amazon.awscdk:s3:${cdkVersion}"
    compile "software.amazon.awscdk:codebuild:${cdkVersion}"
    compile "software.amazon.awscdk:codepipeline:${cdkVersion}"
    compile "software.amazon.awscdk:codepipeline-actions:${cdkVersion}"
    compile "software.amazon.awscdk:secretsmanager:${cdkVersion}"
    testCompile "junit:junit:${junitVersion}"
  }
  application {
    mainClassName = 'software.amazon.samples.EventSourcingPipelineApp'
  }
}

project(':cdk-infra') {
  apply plugin: 'application'
  dependencies {
    compile "software.amazon.awscdk:core:${cdkVersion}"
    testCompile "junit:junit:${junitVersion}"
  }
  application {
    mainClassName = 'software.amazon.samples.EventSourcingInfraApp'
  }
}
